#
# arch-specific
#
CMAKE_MINIMUM_REQUIRED(VERSION 3.20)

IF(NOT DEFINED CMAKE_READELF)
    MESSAGE(FATAL_ERROR "readelf utility not specified")
ENDIF()

IF(NOT DEFINED MODULE_PATH)
    MESSAGE(FATAL_ERROR "Library not specified")
ENDIF()

EXECUTE_PROCESS(
    COMMAND ${CMAKE_READELF} -AW ${MODULE_PATH}
    OUTPUT_VARIABLE ARCH_SPECIFIC_NL
    OUTPUT_STRIP_TRAILING_WHITESPACE    
    COMMAND_ERROR_IS_FATAL ANY
    TIMEOUT 15
)

IF(ARCH_SPECIFIC_NL)
    STRING(REGEX MATCHALL "[^\n\r]+" ARCH_SPECIFIC ${ARCH_SPECIFIC_NL})
    FOREACH(l IN LISTS ARCH_SPECIFIC)
        IF("${l}" MATCHES "^[\t ]*(.+):[\t ]*\"(.+)\"$")
            MESSAGE(STATUS "[arch-specific] ${CMAKE_MATCH_1}: ${CMAKE_MATCH_2}")
        ELSEIF("${l}" MATCHES "^[\t ]*(.+):[\t ]*(.+)$")
            MESSAGE(STATUS "[arch-specific] ${CMAKE_MATCH_1}: ${CMAKE_MATCH_2}")
        ENDIF()
    ENDFOREACH()
ENDIF()

#
# dump-header
#
CMAKE_MINIMUM_REQUIRED(VERSION 3.20)

IF(NOT DEFINED CMAKE_READELF)
    MESSAGE(FATAL_ERROR "readelf utility not specified")
ENDIF()

IF(NOT DEFINED MODULE_PATH)
    MESSAGE(FATAL_ERROR "Library not specified")
ENDIF()

EXECUTE_PROCESS(
    COMMAND ${CMAKE_READELF} -hW ${MODULE_PATH}
    OUTPUT_VARIABLE DUMP_HEADER_NL
    OUTPUT_STRIP_TRAILING_WHITESPACE    
    COMMAND_ERROR_IS_FATAL ANY
    TIMEOUT 15
)

STRING(REGEX MATCHALL "[^\n\r]+" DUMP_HEADER ${DUMP_HEADER_NL})
FOREACH(l IN LISTS DUMP_HEADER)
    IF("${l}" MATCHES "^[\t ]*(.+):[\t ]*\"(.+)\"$")
        MESSAGE(STATUS "[header] ${CMAKE_MATCH_1}: ${CMAKE_MATCH_2}")
    ELSEIF("${l}" MATCHES "^[\t ]*(.+):[\t ]*(.+)$")
        MESSAGE(STATUS "[header] ${CMAKE_MATCH_1}: ${CMAKE_MATCH_2}")
    ENDIF()
ENDFOREACH()


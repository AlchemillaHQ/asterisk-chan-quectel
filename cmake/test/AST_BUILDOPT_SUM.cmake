#
# AST_BUILDOPT_SUM
# Finds specified checksum inside binary file (dynamic library)
#
CMAKE_MINIMUM_REQUIRED(VERSION 3.20)

IF(NOT DEFINED MODULE_PATH)
    MESSAGE(FATAL_ERROR "Library not specified")
ENDIF()

IF(NOT DEFINED AST_BUILDOPT_SUM)
    MESSAGE(FATAL_ERROR "Checksum not specified")
ENDIF()

MESSAGE(DEBUG "DLL:\t${MODULE_PATH}")
MESSAGE(DEBUG "CHECKSUM:\t${AST_BUILDOPT_SUM}")

EXECUTE_PROCESS(
    COMMAND grep -Eao "[0-9abcdef]{32}" ${MODULE_PATH}
    OUTPUT_VARIABLE CHECKSUM_CANDIDATES_ML
    OUTPUT_STRIP_TRAILING_WHITESPACE    
    COMMAND_ERROR_IS_FATAL ANY
    TIMEOUT 15
)

STRING(REGEX MATCHALL "[^\n\r]+" CHECKSUM_CANDIDATES ${CHECKSUM_CANDIDATES_ML})
LIST(LENGTH CHECKSUM_CANDIDATES CHECKSUM_CANDIDATES_CNT)
IF (${CHECKSUM_CANDIDATES_CNT} EQUAL 0)
    MESSAGE(FATAL_ERROR "Could not find checksums in ${MODULE_PATH}")
ENDIF()

SET(TEST_PASSED)
FOREACH(l IN LISTS CHECKSUM_CANDIDATES)
    IF(${l} STREQUAL ${AST_BUILDOPT_SUM})
        MESSAGE(STATUS "[checksum] ${l} ✓")
        SET(TEST_PASSED ON)
    ELSE()
        MESSAGE(STATUS "[checksum] ${l} ⍻")
    ENDIF()
ENDFOREACH()

IF(NOT TEST_PASSED)
    MESSAGE(FATAL_ERROR "Checksum not found")
ENDIF()

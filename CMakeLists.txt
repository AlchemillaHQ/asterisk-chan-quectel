#
# asterisk-chan-quectel
#

CMAKE_MINIMUM_REQUIRED(VERSION 3.14)

#
#
# CHAN_SOURCE_VERSION = git hash
# CHAN_SOURCE_TS = timestamp of ${CHAN_SOURCE_VERSION}
#
# CHAN_VER_MAJOR CHAN_VER_MINOR CHAN_VER_PATCH CHAN_VER_TWEAK
#
# CHAN_STATUS
#

UNSET(OPENWRT_PACKAGE)
UNSET(CHAN_SOURCE_VERSION)
UNSET(CHAN_SOURCE_TS)
UNSET(CHAN_STATUS)

IF(IS_DIRECTORY ${CMAKE_SOURCE_DIR}/.git)
    SET(IS_GIT_REPO "TRUE")
    FIND_PROGRAM(GIT_EXECUTABLE git REQUIRED)
    EXECUTE_PROCESS(
        COMMAND ${GIT_EXECUTABLE} describe --abbrev=6 --dirty --match "v*" --long
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE GIT_DESCRIBE
        RESULT_VARIABLE GIT_ERROR_CODE
        OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    IF(NOT GIT_ERROR_CODE)
        STRING(REGEX MATCH "^v([0-9]+)\\.([0-9]+)\\.([0-9]+)(-([0-9]+)(-.+))?$" PROGRAM_VERSION_MATCH ${GIT_DESCRIBE})
        SET(CHAN_VER_MAJOR ${CMAKE_MATCH_1})
        SET(CHAN_VER_MINOR ${CMAKE_MATCH_2})
        SET(CHAN_VER_PATCH ${CMAKE_MATCH_3})
        SET(CHAN_VER_TWEAK ${CMAKE_MATCH_5})
        set(CHAN_STATUS ${CMAKE_MATCH_6})
        SET(OPENWRT_PACKAGE 1)
    ELSE()
        UNSET(CHAN_STATUS)
        MESSAGE(FATAL_ERROR "Git - unable to describe")
    ENDIF()

    EXECUTE_PROCESS(
        COMMAND ${GIT_EXECUTABLE} log -n 1 "--pretty=format:%H;%ct"
        WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
        OUTPUT_VARIABLE GIT_LOG
        RESULT_VARIABLE GIT_ERROR_CODE
        OUTPUT_STRIP_TRAILING_WHITESPACE
    )
    IF(NOT GIT_ERROR_CODE)
        LIST(GET GIT_LOG 0 CHAN_SOURCE_VERSION)
        LIST(GET GIT_LOG 1 CHAN_SOURCE_TS)
    ELSE()
        MESSAGE(FATAL_ERROR "Git - unable to get timestamp of last commit")
    ENDIF()    
ELSE()
    UNSET(IS_GIT_REPO)
    IF(EXISTS ${CMAKE_SOURCE_DIR}/pkg-vars.cmake)
        INCLUDE(${CMAKE_SOURCE_DIR}/pkg-vars.cmake)
    ENDIF()
ENDIF()

IF(NOT(CHAN_VER_MAJOR AND CHAN_VER_MINOR AND CHAN_VER_PATCH AND CHAN_VER_TWEAK))
    MESSAGE(WARNNG "Unknown channel version - using defaults")
    SET(CHAN_SOURCE_VERSION "NOTFOUND")
    SET(CHAN_SOURCE_TS 0)
    STRING(TIMESTAMP CHAN_VER_MAJOR "%Y" UTC)
    STRING(TIMESTAMP CHAN_VER_MINOR "%m" UTC)
    STRING(TIMESTAMP CHAN_VER_PATCH "%d" UTC)
    STRING(TIMESTAMP CHAN_VER_TWEAK "%H%M" UTC)
    SET(CHAN_STATUS "-unversioned")
ENDIF()

MESSAGE(STATUS "Source version: ${CHAN_SOURCE_VERSION} [${CHAN_SOURCE_TS}]")
MESSAGE(STATUS "Project version: ${CHAN_VER_MAJOR}.${CHAN_VER_MINOR}.${CHAN_VER_PATCH}-${CHAN_VER_TWEAK}${CHAN_STATUS}")

# CMake

IF(OPENWRT_PACKAGE)
    OPTION(OPENWRT_PACKAGE_STANDALONE "OpenWRT standalone package" ON)
ENDIF()
SET(ASTERISK_VERSION_NUM 180000 CACHE STRING "Asterisk version")
OPTION(ICONV_CONST "iconv() const declaration of input buffer" OFF)
SET(ICONV_T iconv_t CACHE STRING "iconv type")
SET(AST_MODULE chan_quectel)

INCLUDE(CheckIncludeFile)
INCLUDE(CheckIncludeFiles)
INCLUDE(CheckSymbolExists)

PROJECT(asterisk-chan-quectel
    VERSION ${CHAN_VER_MAJOR}.${CHAN_VER_MINOR}.${CHAN_VER_PATCH}.${CHAN_VER_TWEAK}
    HOMEPAGE_URL "http://github.com/RoEdAl/asterisk-chan-quectel"
    DESCRIPTION "Asterisk channel driver for Quectel modules"
    LANGUAGES C
)

SET(CMAKE_INSTALL_PREFIX "/usr")

SET(CMAKE_REQUIRED_DEFINITIONS "-DAST_MODULE_SELF_SYM=__internal_chan_quectel_self")
CHECK_INCLUDE_FILE(asterisk.h HAVE_ASTERISK_H)

IF(NOT HAVE_ASTERISK_H)
    MESSAGE(FATAL_ERROR "Asterisk header not found. Cannot continue.")
ENDIF()

CHECK_INCLUDE_FILES("stdarg.h;asterisk.h;asterisk/frame.h" HAVE_ASTERISK_FRAME_H)
CHECK_SYMBOL_EXISTS(AST_CONTROL_SRCCHANGE "stdarg.h;asterisk.h;asterisk/frame.h" HAVE_AST_CONTROL_SRCCHANGE)

CHECK_INCLUDE_FILE(fcntl.h HAVE_FCNTL_H)
CHECK_INCLUDE_FILE(inttypes.h HAVE_INTTYPES_H)

CHECK_INCLUDE_FILE(memory.h HAVE_MEMORY_H)
CHECK_SYMBOL_EXISTS(memchr memory.h HAVE_MEMCHR)
CHECK_SYMBOL_EXISTS(memmove memory.h HAVE_MEMMOVE)
CHECK_SYMBOL_EXISTS(memset memory.h HAVE_MEMSET)

CHECK_INCLUDE_FILE(stdint.h HAVE_STDINT_H)

CHECK_INCLUDE_FILE(stdlib.h HAVE_STDLIB_H)
CHECK_SYMBOL_EXISTS(realpath stdlib.h HAVE_REALPATH)
CHECK_SYMBOL_EXISTS(strtol stdlib.h HAVE_STRTOL)

CHECK_INCLUDE_FILE(string.h HAVE_STRING_H)
CHECK_SYMBOL_EXISTS(memmem string.h HAVE_MEMMEM)

CHECK_INCLUDE_FILE(strings.h HAVE_STRINGS_H)
CHECK_SYMBOL_EXISTS(strcasecmp strings.h HAVE_STRCASECMP)
CHECK_SYMBOL_EXISTS(strncasecmp strings.h HAVE_STRNCASECMP)

CHECK_INCLUDE_FILE(sys/stat.h HAVE_SYS_STAT_H)
CHECK_INCLUDE_FILE(sys/time.h HAVE_SYS_TIME_H)
CHECK_INCLUDE_FILE(sys/types.h HAVE_SYS_TYPES_H)
CHECK_INCLUDE_FILE(termios.h HAVE_TERMIOS_H)
CHECK_INCLUDE_FILE(unistd.h HAVE_UNISTD_H)

SET(THREADS_PREFER_PTHREAD_FLAG ON)
FIND_PACKAGE(Threads REQUIRED)
FIND_PACKAGE(ALSA REQUIRED)
FIND_PACKAGE(SQLite3 REQUIRED)
FIND_PACKAGE(Iconv REQUIRED)

IF(ICONV_CONST)
    SET(ICONV_CONST_STR "const")
ELSE()
    SET(ICONV_CONST_STR "/**/")
ENDIF()

IF(HAVE_AST_CONTROL_SRCCHANGE)
    SET(HAVE_AST_CONTROL_SRCCHANGE_STR 1)
ELSE()
    SET(HAVE_AST_CONTROL_SRCCHANGE_STR "/**/")
ENDIF()

CONFIGURE_FILE(config.h.in include/config.h)

INCLUDE(GNUInstallDirs)

IF(DEFINED OPENWRT_PACKAGE)
    ADD_SUBDIRECTORY(openwrt)
ENDIF()
ADD_SUBDIRECTORY(src)

#
# CPack
# Based on: http://decovar.dev/blog/2021/09/23/cmake-cpack-package-deb-apt/
#

IF(NOT CPACK_PACKAGE_ARCHITECTURE)
    FIND_PROGRAM(DPKG_EXECUTABLE dpkg)
    IF(NOT DPKG_EXECUTABLE)
        MESSAGE(WARNING "Can not find dpkg in your path, setting CPACK_PACKAGE_ARCHITECTURE to amd64.")
        SET(CPACK_PACKAGE_ARCHITECTURE amd64)
    ELSE()
        EXECUTE_PROCESS(
            COMMAND "${DPKG_EXECUTABLE}" --print-architecture
            OUTPUT_VARIABLE CPACK_PACKAGE_ARCHITECTURE
            OUTPUT_STRIP_TRAILING_WHITESPACE
        )
    ENDIF()
ENDIF()

SET(CPACK_PACKAGE_DIRECTORY ${PROJECT_BINARY_DIR}/package)
SET(CPACK_PACKAGE_VERSION "${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}-${PROJECT_VERSION_TWEAK}${CHAN_STATUS}")
SET(CPACK_PACKAGE_FILE_NAME "${CMAKE_PROJECT_NAME}_${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}_${PROJECT_VERSION_TWEAK}${CHAN_STATUS}_${CPACK_PACKAGE_ARCHITECTURE}")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${CMAKE_PROJECT_DESCRIPTION}")
SET(CPACK_PACKAGE_DESCRIPTION "Asterisk channel driver for Quectel and SimCOM modules.")
SET(CPACK_PACKAGE_CONTACT "roed@onet.eu")
SET(CPACK_SET_DESTDIR ON)
SET(CPACK_STRIP_FILES ON)
SET(CPACK_INSTALL_DEFAULT_DIRECTORY_PERMISSIONS
    OWNER_READ OWNER_WRITE OWNER_EXECUTE
    GROUP_READ GROUP_EXECUTE
    WORLD_READ WORLD_EXECUTE
)

SET(CPACK_GENERATOR "TGZ;DEB")
SET(CPACK_DEBIAN_FILE_NAME "${CMAKE_PROJECT_NAME}_${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}_${PROJECT_VERSION_TWEAK}${CHAN_STATUS}_${CPACK_PACKAGE_ARCHITECTURE}.deb")
SET(CPACK_DEBIAN_PACKAGE_MAINTAINER "Edmunt Pienkowsky <${CPACK_PACKAGE_CONTACT}>")
SET(CPACK_DEBIAN_PACKAGE_DEPENDS "alsa-lib, libsqlite3, asterisk")
SET(CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON)
SET(CPACK_DEBIAN_PACKAGE_SECTION comm)
SET(CPACK_DEBIAN_PACKAGE_PRIORITY optional)
SET(CPACK_DEBIAN_PACKAGE_RECOMMENDS "usb-modeswitch")
SET(CPACK_DEBIAN_PACKAGE_ENHANCES "asterisk")

FILE(COPY cpack/deb DESTINATION cpack FILE_PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ)
SET(CPACK_DEBIAN_PACKAGE_CONTROL_EXTRA ${PROJECT_BINARY_DIR}/cpack/deb/conffiles)

SET(CPACK_SOURCE_GENERATOR "TGZ")
SET(CPACK_SOURCE_PACKAGE_FILE_NAME "${CMAKE_PROJECT_NAME}_${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}_${PROJECT_VERSION_TWEAK}${CHAN_STATUS}_src")
SET(CPACK_SOURCE_IGNORE_FILES "/build/;/install/;/test/;/tools/;/\.vscode/;/\.git/;/\.gitignore;/\.gitattributes")

INCLUDE(CPack)
